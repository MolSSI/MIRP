@PACKAGE_INIT@

set(_valid_components
    shared
    static
)


# Find the dependencies
include(${CMAKE_CURRENT_LIST_DIR}/mirpDependencies.cmake)


# set the libraries to use
set(mirp_LIBRARY_BASE "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@")
if(@BUILD_SHARED_LIBS@)
    set(mirp_LIBRARY_NAME ${CMAKE_SHARED_LIBRARY_PREFIX}mirp${CMAKE_SHARED_LIBRARY_SUFFIX})
    set(mirp_shared_FOUND 1)
else()
    set(mirp_LIBRARY_NAME ${CMAKE_STATIC_LIBRARY_PREFIX}mirp${CMAKE_STATIC_LIBRARY_SUFFIX})
    set(mirp_static_FOUND 1)
endif()


# Check for invalid components
foreach(_comp IN LISTS mirp_FIND_COMPONENTS)
    list(FIND _valid_components ${_comp} _is_valid_comp)
    if(${_is_valid_comp} LESS 0)
        message(STATUS "mirpConfig: requested invalid component: ${_comp}")
    endif()
endforeach()


check_required_components(mirp)

#-----------------------------------------------------------------------------
# Don't include targets if this file is being picked up by another
# project which has already built this as a subproject
#-----------------------------------------------------------------------------
if(NOT TARGET mirp::mirp)
    include("${CMAKE_CURRENT_LIST_DIR}/mirpTargets.cmake")
endif()


